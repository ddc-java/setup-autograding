/*
 *  Copyright 2021 CNM Ingenuity, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
    id 'java'
}

sourceCompatibility = 11
targetCompatibility = 11

group 'edu.cnm.deepdive'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

def autogradingDir = 'autograding'

task copyReport(type: Copy) {
    from("${testReportDir}/test") {
        include '**/*'
    }
    into "${testReportDir}/combined/${project.reportName}/"
}

test {
    useJUnitPlatform()
    compileTestJava.enabled = false
    processTestResources.enabled = false
    dependsOn classes
    classpath -= testClassesDirs
    classpath -= files("${buildDir}/resources/test")
    testClassesDirs = files(autogradingDir)
    classpath += testClassesDirs
    testLogging {
        events 'passed', 'failed', 'skipped'
    }
    finalizedBy copyReport
}
